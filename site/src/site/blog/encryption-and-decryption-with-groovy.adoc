= Encryption and decryption with Groovy
Paul King
:revdate: 2022-09-19T14:34:39+00:00
:keywords: decryption, encryption, groovy, security, bouncy castle
:description: This post looks at encrypting and decrypting data using Groovy with the \
AES and CAST5 algorithms.

Inspired by this recent https://asyncq.com/how-to-encrypt-and-decrypt-data-in-java[blog entry], here is an example showing how to encrypt and decrypt with Groovy.

== Using the JDK crypto classes

First, we need some text to encrypt. We'll use an excerpt of the one from the aforementioned blog post:

[source,groovy]
----
var text = 'Contrary to popular belief, Lorem Ipsum is not simply random text. It has \
roots in a piece of classical Latin literature from 45 BC, making it over 2000 years old.'
----

Next, we'll create a factory for our cipher instance, generate a key, and set up an initialization vector.

First, the cipher factory:

[source,groovy]
----
var factory = { Cipher.getInstance('AES/CBC/PKCS5Padding') }
----

For our cipher algorithm, we are using the Advanced Encryption Standard (https://en.wikipedia.org/wiki/Advanced_Encryption_Standard[AES]) algorithm, in Cipher Block Chaining (https://www.highgo.ca/2019/08/08/the-difference-in-five-modes-in-the-aes-encryption-algorithm/[CBC]) mode, with https://en.wikipedia.org/wiki/Padding_(cryptography)#PKCS#5_and_PKCS#7[PKCS5 padding]. We'll look at other options later.

Next we generate our secret key. Our secret key is our password. Only someone who has the password will be able to decrypt the encrypted message. We could use any random bits for our key, but like passwords, we want to choose a strong key rather than a weak one. Cryptographic libraries provide classes to generate such keys. We just need to provide the key size. AES supports 128, 192 and 256-bit keys. We'll choose 192 here:

[source,groovy]
----
var key = generateKey('AES', 192)
----

Our code uses this helper method:

[source,groovy]
----
def generateKey(String algorithm, Integer size) {
    var generator = KeyGenerator.getInstance(algorithm)
    generator.init(size)
    generator.generateKey()
}
----

Next, we generate an initialization vector:

[source,groovy]
----
var ivParameterSpec = randomParameterSpec(factory)
----

It uses this helper method (we're using the algorithm block size for our initialization vector size):

[source,groovy]
----
def randomParameterSpec(Closure<Cipher> factory) {
    var block = new byte[factory().blockSize]
    SecureRandom.instanceStrong.nextBytes(block)
    new IvParameterSpec(block)
}
----

An https://en.wikipedia.org/wiki/Initialization_vector[initialization vector] is used to introduce some additional randomness to avoid repeating patterns in the input leading to repeating patterns in the encrypted bytes.

With all these things in place, we are almost ready to encrypt or decrypt, but first, let's define two more helper methods:

[source,groovy]
----
def encrypt(byte[] bytes, Key key, IvParameterSpec spec, Closure<Cipher> factory) {
    var cipher = factory()
    cipher.init(ENCRYPT_MODE, key, spec)
    cipher.doFinal(bytes)
}

def decrypt(byte[] bytes, Key key, IvParameterSpec spec, Closure<Cipher> factory) {
    var cipher = factory()
    cipher.init(DECRYPT_MODE, key, spec)
    cipher.doFinal(bytes)
}
----

And here is how we encrypt and decrypt:


[source,groovy]
----
var encrypted = encrypt(text.bytes, key, ivParameterSpec, factory)
println "Encrypted bytes : $encrypted"
println "Encrypted text : ${new String(encrypted)}"

var decrypted = decrypt(encrypted, key, ivParameterSpec, factory)
println "Decrypted bytes : $decrypted"
println "Decrypted text : ${new String(decrypted)}"
----

Which has this output:

[source]
----
Encrypted bytes : [-117, 36, 18, 69, -101, -8, 35, 93, -102, -49, -12, â€¦, -19, -100]
Encrypted text : â€¹$Eâ€ºÃ¸#]Å¡ÃÃ´Ã¦â€ÃËœÃ§p^ÂµÂ³=L(Ã–^_Å’C>CIÃ‹â€žÃ¶,1Ã‰8Ã†Å¸.Å ?vÃŸG,Ãˆwâ€°Ã¥Â¼zÃœf>?Âµâ€ºDÂ¹Ã©Ã†kâ‚¬ Â°Ëœ2ÃªÃ”}Ã­Â©Ã hl$>?Â¹Â¡KÃ¥3Ã”O?Â±&â€¦ÃªÃ®Â¶ÃŠâ€“Â¾Â°Â®qÂ®Ã â€”0Ãºâ€˜Ã”hO<HÂ¦Ã§Â®Ã‡â€ÃˆhAÃ«jÃ³ QPyÆ’y6Ã„Â¤*Â´unÂ¼Ã¯Â¯mÂ¨Â´Ã™jeJtÃ«Âº\Ã³6Ã†ÂªKÂªÅ“Ã­Å“
Decrypted bytes : [67, 111, 110, 116, 114, 97, 114, 121, 32, 116, 111, 32, â€¦, 100, 46]
Decrypted text : Contrary to popular belief, Lorem Ipsum is not simply random text. It has roots in a piece of classical Latin literature from 45 BC, making it over 2000 years old.
----

We can see that everything worked as expected, since the final output matches our original input text.

We can alternatively, swap algorithms. There are numerous https://docs.oracle.com/en/java/javase/11/docs/api/java.base/javax/crypto/Cipher.html[algorithms and modes supported] by the JDK and others supported by third-party libraries. A nice summary can be found https://en.wikipedia.org/wiki/Comparison_of_cryptography_libraries[here]. Let's have a look at using a different algorithm.

== Using the Bouncy Castle library

We'll swap to use the CAST5 (https://en.wikipedia.org/wiki/CAST-128[CAST-128]) algorithm
which supports up to a 128-bit key. We'll use https://en.wikipedia.org/w/index.php?title=HMAC-SHA1[HMAC-SHA1] to generate our key.

[source,groovy]
----
import org.bouncycastle.jce.provider.BouncyCastleProvider
var bc = new BouncyCastleProvider()
factory = { Cipher.getInstance('CAST5', bc) }
key = generateKey('HmacSHA1', 128)
ivParameterSpec = randomParameterSpec(factory)
----

CAST5 is the default algorithm used in some versions of GPG and PGP.
It isn't included by default in the JDK, so for this we'll use the
https://www.bouncycastle.org/java.html[Bouncy Castle library]. +

_NOTE_
****
Just as an aside, if you are wanting to encrypt or decrypt GPG/PGP files, don't use the above code. Libraries like Bouncy Castle have https://www.bouncycastle.org/docs/pgdocs1.8on/index.html[dedicated classes] for such scenarios.
****

We now encrypt and decrypt as before:

[source,groovy]
----
encrypted = encrypt(text.bytes, key, ivParameterSpec, factory)
println "Encrypted text : ${new String(encrypted)}"
decrypted = decrypt(encrypted, key, ivParameterSpec, factory)
println "Decrypted text : ${new String(decrypted)}"
----

Which has this output:

[source]
----
Encrypted text : MÂªÃ¡?r?v9Â£Ã·~4ÂµT'â€ºÃ™ÃÃlÂ¿ÃžgÂ¾0Ã±Å½Â¡?Ãœ=Â³9QÂ¬Â»3Â«Ã–ÃÂ¡Âµ Â¾@4Ã·`FÃ±Ã™Å fÃ¸7Â¥#â€ºvÂ¤Ãâ€“â€°Â¼ÃœÂ¢Æ’E6Ã´Å½TÃ™lÃ¦Ãz>o?Ã Lâ€ºÂ¡Â¢z1nÃ–o9]Å¡OÃ”Â¼SÃ”OÃ#Ã7LÅ“Ã€Ã®}Ã³5m%qâ€¢Â»l%/AWTÂ´Â¢zH#tÃ¬â€ Â±lÂ¶Â£â€”Å’Â«Â©wË†ÃƒÂ®>Â®Ãœ6Ã«r-E
Decrypted text : Contrary to popular belief, Lorem Ipsum is not simply random text. It has roots in a piece of classical Latin literature from 45 BC, making it over 2000 years old.
----

== Other useful functionality

Passing around binary data like our secret key or the encrypted data, has its own problems. Groovy provides extension methods to `encode` such data (and corresponding `decode` methods). For example, we can encode our secret key in various ways:

[source,groovy]
----
var keyBytes = key.encoded
println keyBytes.encodeHex()
println keyBytes.encodeBase64()
println keyBytes.encodeBase64Url()
----

Which has this output (the key is random, so the output will differ for each run):

[source]
----
85a0d3f0ce0cbe6402dc9579fbffcf1d
haDT8M4MvmQC3JV5+//PHQ==
haDT8M4MvmQC3JV5-__PHQ
----

Groovy also provides extension methods for various checksums (but you might want to look at stronger checksum algorithms in security sensitive scenarios):

[source,groovy]
----
println "SHA256 : ${text.sha256()}"
println "MD5 : ${text.md5()}"
----

Which has this output:

[source]
----
SHA256 : ccb184e35e4c32bafc730d84ec924ea2980035ea5fadb012e3b2b31abf4323c9
MD5 : 46c61a174c2dc99204521ca89f09f63c
----

If you are encrypting and decrypting entire files, the JDK has special classes for that too which are also easy to use from Groovy. That's all for now.

== References

* https://en.wikipedia.org/wiki/Comparison_of_cryptography_libraries[Comparison of cryptography libraries] (Wikipedia)
* https://asyncq.com/how-to-encrypt-and-decrypt-data-in-java[How to encrypt and decrypt data in Java] (also on https://i-sammy.medium.com/how-to-encrypt-and-decrypt-data-in-java-de41be237422[medium.com])
* https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk18on/1.71.1[Bouncy castle] in maven central
* https://www.baeldung.com/java-aes-encryption-decryption[AES Encryption and Decryption in Java] (Baeldung)
* https://www.baeldung.com/java-encryption-iv[Initialization Vector for Encryption in Java] (Baeldung)
* https://www.baeldung.com/java-3des[3DES in Java] (Baeldung)

== Conclusion

We have taken a brief look at encrypting and decrypting with Apache Groovy.
